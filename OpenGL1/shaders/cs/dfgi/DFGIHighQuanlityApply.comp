#version 450 core
#extension GL_EXT_shader_atomic_float : enable
#extension GL_EXT_transform_feedback : enable
#pragma debug(on)
const int size=16;

layout(local_size_x = size, local_size_y = 1, local_size_z=1) in;

struct DFGIRay{
    vec3 direction;
    vec3 hitPosition;
    vec3 flux;

    uint refCount;
    uint liveCount;
};

layout(std140 , binding=0) uniform CameraProperty{
    mat4 model;       //0
    mat4 view;        //64
    mat4 projection;  //128
    mat4 mvp;         //192
    mat4 lastMvp;     //256
    vec3 CameraPos;   //320
    vec2 Offset;      //336  272 
};

layout(binding = 1, rgba32f) uniform image2D DFGIResult;
uniform sampler2D gPositionRoughness;
uniform sampler2D gNormalDepth;
uniform sampler2D gAlbedoMetallic;
uniform vec3 SceneGridsResolution;
uniform vec3 GlobalSDFBoxMin; //vec3(-17.4,-1.7,-7.8);
uniform vec3 GlobalSDFBoxMax; //vec3(17.4,15.6,7.8);
uniform bool NeedMarkGridCanMultBounce;

layout(std430, binding = 2) buffer DFGIRaysBuffer{
    DFGIRay rays[];
};

layout(std430, binding = 3) buffer DFGIGridCanSparse{
    uint gridCanSparseIndex[];
};

vec3 WorldPosToUVW(vec3 worldPos){
    return (worldPos-GlobalSDFBoxMin)/(GlobalSDFBoxMax-GlobalSDFBoxMin);
}

ivec3 WorldPosToGridIndex(vec3 worldPos){
    vec3 uvw=WorldPosToUVW(worldPos);
    return ivec3(SceneGridsResolution.x*uvw.x,SceneGridsResolution.y*uvw.y,SceneGridsResolution.z*uvw.z);
}

uint LinearGridIndex(ivec3 gridIndex){
    return uint(gridIndex.z*(SceneGridsResolution.x*SceneGridsResolution.y)+gridIndex.y*SceneGridsResolution.x+gridIndex.x);
} 

const float PI = 3.14159265359;

float DistributionGGX(float NdotH, float roughness){
    float a=roughness;
    float ggx=a*a/(PI*pow(NdotH*NdotH*(a*a-1.0)+1.0,2.0));
    return ggx;
}

float GeometrySchlickGGX(float NdotV, float roughness){
    float k=pow(1.0+roughness,2.0)/8.0;
    float g=NdotV/(NdotV*(1.0-k)+k);
    return g;
}

float GeometrySmith(float NdotV,float NdotL, float roughness){
    return GeometrySchlickGGX(NdotV,roughness)*GeometrySchlickGGX(NdotL,roughness);
}

vec3 fresnelSchlick(vec3 F0, float VdotH){
    return F0+(1.0-F0)*pow(1.0-VdotH,5.0);
}

float saturate(float a){
    if(a<0)
        return 0.0;
    if(a>1)
        return 1.0;
    return a;
}

vec3 calcMicroFacedBRDF(vec3 N,vec3 L,vec3 V,vec2 tex){
    
    vec3 albedo=texture(gAlbedoMetallic,tex).xyz;
    float rough=texture(gPositionRoughness,tex).w;
    //rough=0.5;
    vec3 F0 = vec3(0.04); 
    float metallic=texture(gAlbedoMetallic,tex).w;
    //metallic=0.5;
    F0 = mix(F0, albedo, metallic);

    vec3 H = normalize(V + L);
    float NdotL = max(dot(N, L), 0.001); 
    float NdotV = max(dot(N, V), 0.001);
    float VdotH = max(dot(V,H),0.001);
    float NdotH = max(dot(N,H),0.001);

    float NDF = DistributionGGX(NdotH, rough);   
    float G   = GeometrySmith(NdotL,NdotV, rough); 
    vec3 F = fresnelSchlick(F0, VdotH);

    vec3 numerator    = NDF * G * F; 
    float denominator = (4.0 * NdotL * NdotV);
    vec3 BRDF = numerator / vec3(denominator,denominator,denominator);
    return BRDF;
}

shared int Lox;
shared int Loy;
shared int Loz;
shared uint cnt;

bool UVWIsValid(vec3 uvw){
    return uvw.x>=0.0&&uvw.x<=1.0
        &&uvw.y>=0.0&&uvw.y<=1.0
        &&uvw.z>=0.0&&uvw.z<=1.0;
}

void main(){
    ivec2 uvIndex = ivec2(gl_WorkGroupID.xy);
    uint rayOffset=gl_LocalInvocationID.x;
    vec2 uv=vec2(uvIndex)/vec2(800,450);
    vec3 worldPos=texture2D(gPositionRoughness,uv).xyz;
    vec3 N=texture2D(gNormalDepth,uv).xyz;
    if(N.x==0&&N.y==0&&N.z==0){
        imageStore(DFGIResult,uvIndex,vec4(0));
        return;
    }

    vec3 albedo=texture2D(gAlbedoMetallic,uv).xyz;
    vec3 V=normalize(CameraPos-worldPos); 

    if(rayOffset.x==0){
        Lox=0;
        Loy=0;
        Loz=0;
        cnt=0;
    }
    groupMemoryBarrier(); memoryBarrierShared(); barrier();

    ivec3 gridIndex=WorldPosToGridIndex(worldPos);
    uint linearGridIndex=LinearGridIndex(gridIndex);
    if(NeedMarkGridCanMultBounce)
        gridCanSparseIndex[linearGridIndex]=1;
    
    uint rayIndex=linearGridIndex*16+rayOffset;
    DFGIRay ray=rays[rayIndex];
    vec3 gridSize=(GlobalSDFBoxMax-GlobalSDFBoxMin)/SceneGridsResolution;
    float attenuation=1-length(worldPos-ray.hitPosition)/gridSize.x;
    vec3 Li=ray.flux*attenuation;
    float NdotL=max(0,dot(N,-ray.direction));
    vec3 Loi=calcMicroFacedBRDF(N,-ray.direction,V,uv)*Li*NdotL;

    atomicAdd(Lox,int(Loi.x*4024));
    atomicAdd(Loy,int(Loi.y*4024));
    atomicAdd(Loz,int(Loi.z*4024));
    atomicAdd(cnt,1U);
    groupMemoryBarrier(); memoryBarrierShared(); barrier();

    if(rayOffset.x==0){
        vec3 Lo=vec3(Lox,Loy,Loz)/4024/float(cnt);
        vec3 col=imageLoad(DFGIResult,uvIndex).xyz;
        imageStore(DFGIResult,uvIndex,vec4(mix(col,Lo,0.05),1));
    }
}
